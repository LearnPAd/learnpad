##START## QUERY:linemanagerAsExpert
##COMMENT:The line manager of the organisational unit recommended as expert
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bpmn: <http://ikm-group.ch/archiMEO/BPMN#>  
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix eo: <http://ikm-group.ch/archiMEO/eo#>
prefix lpd: <http://learnpad.eu#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?name ?firstName ?lastName ?businessActor ?email ?phone ?roleName ?organisationalUnitName
WHERE {
    ?performer emo:performerHasEmailAddress ?userId .
    ?performer omm:performerHasRole ?role .
    ?role lpd:roleIsCastedByOrgUnit ?organisationalUnit .
    ?organisationalUnit eo:organisationalUnitIsHeadedByPerson ?lineManager .
    ?organisationalUnit rdfs:label ?organisationalUnitName .
    ?lineManager rdfs:label ?name .    
    ?lineManager emo:performerHasEmailAddress ?email .
    ?lineManager emo:performerRepresentsPerson ?businessActor .
	FILTER(?userId != ?email)
    OPTIONAL {
        ?lineManager omm:performerHasFirstName ?firstName .
        ?lineManager omm:performerHasLastName ?lastName .
        ?lineManager omm:performerHasPhoneNumber ?phone .
    } .
}
##END##
##START## QUERY:expertsWithSameRole
##COMMENT:Expert in another organisational unit with the same role (name) assigned
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bpmn: <http://ikm-group.ch/archiMEO/BPMN#>  
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix lpd: <http://learnpad.eu#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?name ?performerInOtherUnit ?firstName ?lastName ?businessActor ?email ?phone ?roleName
WHERE {
    ?performer emo:performerHasEmailAddress ?userId .
    ?performer omm:performerHasRole ?role .
    ?role rdfs:label ?roleName .
    ?sameRoleInOtherUnit rdfs:label ?roleName .
    ?performerInOtherUnit omm:performerHasRole ?sameRoleInOtherUnit .
    ?performerInOtherUnit rdfs:label ?name .
    ?performerInOtherUnit emo:performerHasEmailAddress ?email .
    FILTER (?sameRoleInOtherUnit != ?role) .
    ?performerInOtherUnit emo:performerRepresentsPerson ?businessActor .
    OPTIONAL {
        ?performerInOtherUnit omm:performerHasFirstName ?firstName .
        ?performerInOtherUnit omm:performerHasLastName ?lastName .
	?performerInOtherUnit omm:performerHasPhoneNumber ?phone .
    } .
}
##END##
##START## QUERY:expertMostOftenExecutedTask
##COMMENT:Expert who has executed the same task most often
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bpmn: <http://ikm-group.ch/archiMEO/BPMN#>  
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix lpd: <http://learnpad.eu#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
prefix transfer: <http://learnpad.eu/transfer#>
SELECT DISTINCT ?name ?businessActor ?email ?phone ?roleName ?organisationalUnitName
WHERE {
  {
    SELECT ?experiencedPerformer (count(?executedTaskInstance) AS ?count)	
      WHERE {
          ?executedTaskInstance rdf:type ?artifactId .
          ?executedTaskInstance emo:activityIsPerformedByPerformer ?experiencedPerformer .
          ?currentPerformer emo:performerHasEmailAddress ?userId .
          FILTER(?currentPerformer != ?experiencedPerformer)
      } GROUP BY ?experiencedPerformer
    }
    FILTER(?count > 0)
    ?experiencedPerformer rdfs:label ?name .
    ?experiencedPerformer emo:performerHasEmailAddress ?email .
    ?experiencedPerformer emo:performerRepresentsPerson ?businessActor .
    ?experiencedPerformer omm:performerHasRole ?role .
    ?role rdfs:label ?roleName .
    ?role lpd:roleIsCastedByOrgUnit ?organisationalUnit .
    ?organisationalUnit rdfs:label ?organisationalUnitName .
    OPTIONAL {
        ?businessActor foaf:phone ?phone .
    } .
} ORDER BY DESC (?count ) LIMIT 1
##END##
##START## QUERY:learningObjectsForRestructuringInProtectedLandscapeArea
##COMMENT:Recommend documents relevant for restructuring applications in a protected landscape area
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bpmn: <http://ikm-group.ch/archiMEO/BPMN#>  
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix lpd: <http://learnpad.eu#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
SELECT ?documentTitle ?documentPageRef 
WHERE {
    ?taskInstance rdf:type bpmn:Task .
    ?executedTaskInstance rdf:type ?taskInstance .
    ?executedTaskInstance eo:atomicBusinessProcessProcessesDataObject ?applicationData .
    ?applicationData lpd:applicationDataRequestsForActivity lpd:RestructuringBusinessActivity .
    ?applicationData lpd:applicationDataRequestsForArea lpd:ProtectedLandscapeArea .
    ?lpd:RestructuringBusinessActivity lpd:documentsUsedForPerformingBusinessActivity ?document .
    ?document emo:documentRepresentsdocument ?foafDocument .
    ?foafDocument elements:documentHasTitle ?documentTitle .
    ?foafDocument eo:documentHasStorage ?documentNode .
    ?documentNode lpd:xwikiPageRepresentsNode ?documentPageRef .
}	
##END##
##START## QUERY:learningMaterialForNextCompetencyLevel
##COMMENT:Recommend learning material related to the competency level above the users comptency level considering the learning style
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bpmn: <http://ikm-group.ch/archiMEO/BPMN#>
prefix cmm: <http://ikm-group.ch/archiMEO/CMM#>
prefix dkm: <http://ikm-group.ch/archiMEO/dkm#>
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix lpd: <http://learnpad.eu#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
SELECT ?learningMaterialDocument ?documentLabel ?documentURL ?documentMimeType ?documentDescription ?documentComment
WHERE {
    {
        SELECT DISTINCT ?requiredCompetency ?requiredCompetenciesEQFLevel
        WHERE {
            ?performer emo:performerHasEmailAddress ?userId .
            ?performer omm:performerAcquiredCompetencyProfile  ?aquiredCompetencyProfile .
            ?aquiredCompetencyProfile cmm:competenciesAndLevels ?aquiredCompetencyAndLevel .
            ?aquiredCompetencyAndLevel cmm:competencyAndLevelLevel ?aquiredCompetencyLevelNumber .
            ?aquiredCompetencyAndLevel cmm:competencyAndLevelRefersToCompetency ?aquiredCompetency .
            ?performer omm:performerHasRole ?role .
            ?role omm:roleRequiresCompetencyProfile ?requiredCompetencyProfile .
			?requiredCompetencyProfile cmm:competenciesAndLevels ?requiredCompetencyLevel .
			?aquiredCompetencyAndLevel cmm:competencyAndLevelLevel ?requiredCompetencyLevelNumber .
			?aquiredCompetencyAndLevel cmm:competencyAndLevelRefersToCompetency ?requiredCompetency .
            FILTER(?aquiredCompetency = ?requiredCompetency) .
			?requiredCompetency cmm:eqfCompetencyHasLevel ?requiredCompetenciesEQFLevel .
            BIND(?requiredCompetenciesEQFLevel - ?aquiredCompetencyLevelNumber AS ?levelDiff) .
            FILTER (?levelDiff  = 1) .
        } 
    }
    ?learningMaterialDocument dkm:learningDocumentIncreasesCompetenciesToLevel ?competencyAndLevelOfLearningMaterial.
    ?competencyAndLevelOfLearningMaterial cmm:competencyAndLevelRefersToCompetency ?requiredCompetency .
    ?competencyAndLevelOfLearningMaterial cmm:competencyAndLevelLevel  ?requiredCompetenciesEQFLevel .
    ?learningMaterialDocument dkm:documentHasURL ?documentURL .
    ?learningMaterialDocument dkm:documentHasMIMEType ?documentMimeType .
    ?learningMaterialDocument rdfs:label ?documentLabel .
    OPTIONAL {?learningMaterialDocument rdfs:comment ?documentComment . }
    OPTIONAL {?learningMaterialDocument dkm:documentHasDescription ?documentDescription . }
}
##END##
##START## QUERY:documentsOrWikiPagesOfSameAuthor
##COMMENT:Retrieves documents or wiki pages created by the given author
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dcterms#>
prefix dkm: <http://ikm-group.ch/archiMEO/dkm#>
prefix elements: <http://purl.org/dc/elements/1.1#>
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
prefix xwiki: <http://www.xwiki.org/xwiki/bin/view/Main/WebHome#>
SELECT DISTINCT ?documentURI ?documentLocationURL ?documentMimeType ?documentLabel ?documentComment ?documentDescription ?documentLocationURL ?lastUpdated
WHERE {
  OPTIONAL{
    ?author emo:performerHasEmailAddress ?userId .
    ?document elements:documentHasContributor ?author.
    ?document dkm:documentHasURL ?documentLocationURL .
    ?document dkm:documentHasMIMEType ?documentMimeType .
    ?document rdfs:label ?documentLabel .
    OPTIONAL {?document dcterms:documentHasModifiedDate ?lastUpdated .} 
    OPTIONAL {?document rdfs:comment ?documentComment . }
    OPTIONAL {?document dkm:documentHasDescription ?documentDescription . }
    BIND(str(?document) AS ?documentURI) .
  }
  OPTIONAL{
    ?author emo:performerHasEmailAddress ?userId .
    ?document  xwiki:pageHasCreator ?author .
    ?document xwiki:pageHasCreationTimestamp ?creationTime .
    ?document rdfs:label ?documentLabel .
    BIND(str(?document) AS ?documentLocationURL) .
    BIND("text/html"^^xsd:string AS ?documentMimeType) .
    BIND(str(?document) AS ?documentURI) .
  }
}
##END##
##START## QUERY:documentsOrWikiPagesOfSameAuthorOfOtherDocument
##COMMENT:Retrieves documents or wiki pages created by the same author as of an given document
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dcterms#>
prefix dkm: <http://ikm-group.ch/archiMEO/dkm#>
prefix elements: <http://purl.org/dc/elements/1.1#>
prefix emo: <http://ikm-group.ch/archiMEO/emo#>
prefix omm: <http://ikm-group.ch/archiMEO/omm#>
prefix foaf: <http://xmlns.com/foaf/spec#>
prefix xsd:  <http://www.w3.org/2001/XMLSchema#>
prefix xwiki: <http://www.xwiki.org/xwiki/bin/view/Main/WebHome#>
SELECT DISTINCT ?documentURI ?documentLocationURL ?documentMimeType ?documentLabel ?documentComment ?documentDescription ?documentLocationURL ?lastUpdated
WHERE {
  OPTIONAL{
    ?orgDocument dkm:documentHasURL ?orgDocumentLocationURL .
    ?orgDocument elements:documentHasContributor ?author.
    ?document elements:documentHasContributor ?author.
    ?document dkm:documentHasURL ?documentLocationURL .
    ?document dkm:documentHasMIMEType ?documentMimeType .
    ?document rdfs:label ?documentLabel .
    FILTER(?orgDocument != ?document)
    OPTIONAL {?document dcterms:documentHasModifiedDate ?lastUpdated .} 
    OPTIONAL {?document rdfs:comment ?documentComment . }
    OPTIONAL {?document dkm:documentHasDescription ?documentDescription . }
    BIND(str(?document) AS ?documentURI) .
  }
  OPTIONAL{
    ?orgDocument dkm:documentHasURL ?orgDocumentLocationURL .
    ?orgDocument elements:documentHasContributor ?author.
    ?document  xwiki:pageHasCreator ?author .
    ?document xwiki:pageHasCreationTimestamp ?creationTime .
    ?document rdfs:label ?documentLabel .
    FILTER(?orgDocument != ?document)
    BIND(str(?document) AS ?documentLocationURL) .
    BIND("text/html"^^xsd:string AS ?documentMimeType) .
    BIND(str(?document) AS ?documentURI) .
  }
}
##END##
##START## QUERY:documentsOrWikiPagesOfTaskContext
##COMMENT:Retrieves documents or wiki pages assigned to a given task (context).
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dcterms#>
prefix dkm: <http://ikm-group.ch/archiMEO/dkm#>
SELECT DISTINCT ?documentURI ?documentLocationURL ?documentMimeType ?documentLabel ?documentComment ?documentDescription ?documentLocationURL ?lastUpdated
WHERE {
  OPTIONAL{
    ?orgDocument dkm:documentHasURL ?orgDocumentLocationURL .
    ?orgDocument dkm:documentSupportsTask ?artifactId .
    ?document dkm:documentSupportsTask ?artifactId .
    ?document dkm:documentHasURL ?documentLocationURL .
    ?document dkm:documentHasMIMEType ?documentMimeType .
    ?document rdfs:label ?documentLabel .
    FILTER(?orgDocument != ?document)
    OPTIONAL {?document dcterms:documentHasModifiedDate ?lastUpdated .} 
    OPTIONAL {?document rdfs:comment ?documentComment . }
    OPTIONAL {?document dkm:documentHasDescription ?documentDescription . }
    BIND(str(?document) AS ?documentURI) .
  }
}
##END##